# todo, actually, stock debian on x220c lacked openssh-server aka sshd, so install that optionally if added to augments
# for now, only used to manage sshd in termux on android devices
body file control
{
  namespace => "personal";
}

bundle agent sshd
{
  methods:
    termux::
      "${this.bundle}_main"
        ifvarclass => "${personal:personal.users}_${this.bundle}_task";
}

bundle agent sshd_main
{

  classes:
    termux::
      "packages_installed" expression => and("openssh_installed", "termux_services_installed", "runit_installed");
    packages_installed::
      # todo maybe don't need sshd_service_installed since sshd_service_enabled handles that case
      "sshd_service_installed" expression => fileexists("${svdir}/sshd/run");
      "sshd_service_running" expression => and("sshd_service_installed",
        not(fileexists("${svdir}/sshd/down")));

  vars:
    "svdir" string => "/data/data/com.termux/files/usr/var/service";
    "public_key" string => execresult("$(sys.cf_secret) decrypt $(this.promise_dirname)$(const.dirsep)id_rsa.pub.secret -o -", noshell);

  packages:
    termux::
      "openssh" policy => "present", classes => if_ok("openssh_installed");
      "termux-services" policy => "present", classes => if_ok("termux_services_installed");
      "runit" policy => "present", classes => if_ok("runit_installed");

  files:
    "/data/data/com.termux/files/home/.ssh/authorized_keys"
      create => "true",
      edit_line => default:lines_present("${public_key}");

    "${default:paths.etc_path}/ssh/sshd_config"
      edit_line => default:delete_lines_matching("PasswordAuthentication yes");
    "${default:paths.etc_path}/ssh/sshd_config"
      edit_line => default:lines_present("PasswordAuthentication no");

  commands:
    # sv-enable enables the service as well as starts it running, easier than checking sv status sshd I suppose
    termux.!sshd_service_running::
      "sv-enable sshd" contain => default:in_shell;
}

# also todo, modify sshd_config
# PasswordAuthentication no
#  $PREFIX/etc/ssh/sshd_config
# except we don't get PREFIX but I think I added it somewhere in MPF
