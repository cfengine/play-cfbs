bundle agent docker_inventory
{
  vars:
    any::
      "docker" string => "/usr/local/bin/docker";
    have_docker::
      "status_list" slist => { "created", "exited", "dead" };
      "status_count[${status_list}]" int => int(execresult(`${docker} ps -a --filter "status=${status_list}" --format "{{.Names}}" | wc -l`, "useshell", "stdout")),
        meta => { "inventory", "attribute_name=Docker ${status_list} Count" };
      "images_string" string => execresult(`${docker} images --format "{{.Repository}}:{{.Tag}}"`, "noshell", "stdout");
      "images_list" slist => splitstring("${images_string}", "\n", "inf");
      "images_array[${images_list}]" string => "${images_list}",
        meta => { "inventory", "attribute_name=Docker Image List" };

  classes:
    "have_docker" expression => fileexists("${docker}"),
      scope => "namespace",
      meta => { "inventory", "attribute_name=Docker Installed" };
# alt: tag with report

  reports:
    have_docker::
      "images_array: ${with}" with => storejson(images_array);
      "docker containers ${status_list}: ${status_count[${status_list}]}";
    any::
      "hi from ${this.bundle}";
}

bundle agent __main__
{
  methods: "docker_inventory";
}
